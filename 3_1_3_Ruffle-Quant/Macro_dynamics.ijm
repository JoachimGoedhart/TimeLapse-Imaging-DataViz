//ImageJ macro that extracts dynamic elements from a stack (typically from time-lapse imaging)
//It takes a stack (TIFF) as input (It assumes its name is a file with extension)
//The number of images in the stack should be a multiple of 10 (or the group_size=10 should be modified).
//The output is a map with dynamics which is saved in the same directory as the original file
//A new 'grouped' stack with the dynamics is also shown (but not saved automatically)
//The macro is generated by Joachim Goedhart (2019) with input from Marten Postma

//Defines the number images that are grouped
group_size=10

//Retrieve image name and remove extension
imagename=getTitle()
dotIndex = indexOf(imagename, "."); 
title = substring(imagename, 0, dotIndex); 
//Retrieve directory of the image for saving the result
filedir = getDirectory("image"); filein=filedir+getTitle();
fileout=filedir+title+"_dynamics-map"
//Correct for bleaching
run("Bleach Correction", "correction=[Exponential Fit]");
rename("Bleach_Corrected");
//run("Z Project...", "projection=[Standard Deviation]");
run("Grouped Z Project...", "projection=[Standard Deviation] group=group_size");
rename("SD");
selectWindow("Bleach_Corrected");
run("Grouped Z Project...", "projection=[Average Intensity] group=group_size");
//run("Z Project...", "projection=[Average Intensity]");
rename("AVG");
imageCalculator("Divide create 32-bit stack", "SD","AVG");
selectWindow("Result of SD");
rename("Dynamics");
run("Z Project...", "projection=[Average Intensity]");
rename("Dynamics-map");
selectWindow("Dynamics-map");
run("morgenstemning");
//run("Brightness/Contrast...");
run("Enhance Contrast", "saturated=0.3");
//Save the map with dynamics in false colors (32 bit Tiff)
saveAs("Tiff", fileout);
selectWindow("AVG");
close();
selectWindow("Bleach_Corrected");
close();
selectWindow("SD");
close();
selectWindow("y = a*exp(-bx) + c");
close();

